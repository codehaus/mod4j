preventMMGeneration

importMetamodel "http://www.mod4j.org/service" ;


ServiceModel:
	(description=STRING )?
	"Service" name = ID
	(dtoReferences+=DtoReference)*
	(
	    (crudServices += CrudService)
	    | (methods+= ServiceMethod)
	)*
	;

Enum MethodType:
     CREATE="create"  | READ="read" | UPDATE="update" | DELETE="delete" 
     ;

DtoReference:
	(description=STRING )?
    "from" modelname=ID "import" name=ID 
    ;

ServiceMethod :
	(description=STRING )?
    (CustomMethod | SpecialMethod)
    ;
         	
CustomMethod:
	"method"
	name=ID
	("in"  (inParameters+=Parameter)*  )?
	("out" output=[DtoReference] )?
    ";" ;
     
Parameter:
    type=[DtoReference]
    name=ID 
    ;
    
SpecialMethod:
    type=MethodType
    name=ID
    "for" dto = [DtoReference]
    ";"
    ;
    
CrudService:
	(description=STRING )?
    name="crud" "for" dto =[DtoReference]
    ";"
    ;

