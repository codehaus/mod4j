/*******************************************************************************
 * Copyright (c) 2009 Ordina and committers to Mod4j
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Ordina - initial implementation
 *******************************************************************************/
preventMMGeneration

importMetamodel "http://www.mod4j.org/service" ;


ServiceModel:
	(description=STRING )?
	"service" name = ID ";"
	(dtoReferences+=DtoReference)*
	(
	    (crudServices += CrudService)
	    | (methods+= ServiceMethod)
	)*
	;

AssociationMethod :
	(description=STRING )?
    "for" main=[DtoReference]  "reference"  rolename=ID 
    type=AssociationMethodType
    part=[DtoReference] ";"
	;
Enum SpecialMethodType [MethodType]:
     CREATE="create"  | READ="read" | UPDATE="update" | DELETE="delete" | LISTALL="listall" | FIND="find"
     ;

Enum AssociationMethodType [MethodType] :
     ADDTO="add" | REMOVEFROM="remove" // | GETFROM="get"
     ;

Enum AssociationToMethodType [MethodType] :
     ADDTO="add" 
     ;

Enum AssociationFromMethodType [MethodType] :
     REMOVEFROM="remove" 
     ;

Enum AssociationGetMethodType [MethodType] :
     GETFROM="get" 
     ;

DtoReference:
    "from" modelname=ID "import" name=ID ";"
    ;

ServiceMethod :
	(description=STRING )?
    (CustomMethod | SpecialMethod | AssociationMethod )
    ;
    	
CustomMethod:
	(description=STRING )?
	"method"
	name=ID
	("in" "[" (inParameters+=Parameter ";")* "]" )?
	("out" output=[DtoReference] )?
    ";" ;
     
Parameter:
    type=[DtoReference]
    name=ID 
    ;
    
SpecialMethod:
	(description=STRING )?
    "for" dto = [DtoReference]
    type=SpecialMethodType
    name=ID
    ";"
    ;
    
CrudService:
	(description=STRING )?
    "for" dto =[DtoReference]
    name="crud"
    ";"
    ;

