/* 
   This file contains the code to create a Crossx model from a BusinesDomainDsl model.
   Needed for crosssreference purposes.
*/

import crossx;
import DataContractDsl;

extension org::mod4j::crossx::broker::xtend::Crossx;
extension org::mod4j::dslcommon::xtend::generatorutil;
extension org::mod4j::eclipse::xtend::EclipseUtil;

// Complete the model such that all higher level constructs and all implicit definitions are added.
completeModel(DataContractModel dtcModel) :
    let emptyCrud  = dtcModel.dtos.typeSelect(BusinessClassDto).select(dto|dto.propertyReferences.isEmpty) :
    let filledCrud = dtcModel.dtos.typeSelect(BusinessClassDto).select(dto|!dto.propertyReferences.isEmpty) :
    let associationRoles = dtcModel.dtos.typeSelect(BusinessClassDto).associationReferences :
	emptyCrud.addProperties() ->
	filledCrud.completeProperties()->
	associationRoles.addDtoAssociationRoles()->
	dtcModel.resolveEnumerationReferences()->
	dtcModel.enumerations.addLiterals()
    ;
    
// Add the enumeration literals to the enumeration.
// Literals are always implicitly taken from the businessclassmodel

addLiterals(EnumerationDto enum) :
    let symbol = lookupSymbol(enum.base.modelname, enum.base.name, "Enumeration") :
    let props = symbol.subSymbols.select(s|s.type.matches("EnumerationLiteral")) :
//    print("Enum ["+ enum.name + "]" )->
//    print("Enum symbol ["+ symbol + "]" )->
//    print("Literal symbol ["+ props + "]" )->
//    print("literals ["+ enum.literals.size + "]" )->
    enum.literals.addAll( props.toEnumLiteral()   )->
    null
    ;

EnumerationDtoLiteral toEnumLiteral(Symbol literalProperty) :
   let result = (      //print("TO ENUM LITERAL [" + literalProperty.name + "]")->
        new EnumerationDtoLiteral) :
   result.setName( literalProperty.name )->
   result
   ;
   
// resolvbe all references from DtoEnumerationProperties to their type
resolveEnumerationReferences(DataContractModel dtcModel) :
    let enumProps = dtcModel.dtos.typeSelect(BusinessClassDto).properties.typeSelect(DtoEnumerationProperty)
                    .select(prop | prop.type == null ) :
    enumProps.collect( prop | prop.setType( dtcModel.enumerations.select(e | e.name == prop.dataType).first() ) );
        
addProperties(BusinessClassDto dto) :
    let symbol = lookupSymbol(dto.base.modelname, dto.base.name, "BusinessClass") :
    let props = symbol.subSymbols.select(s|s.type.matches("Property")) :
//    print("DTO addProperties: "+ dto.name)->
    dto.properties.addAll( props.toDtoProperty(dto) ) 
    ;

DataContractDsl::DtoProperty toDtoProperty(Symbol symbol, BusinessClassDto dto) :
//    print("     DTO addProperty: "+ symbol.name)->
    switch (symbol.getPropertyValue("DataType")) {
        case 'boolean' : newBooleanProperty(symbol)
        case 'string' : newStringProperty(symbol)
        case 'integer' : newIntegerProperty(symbol)
        case 'decimal' : newDecimalProperty(symbol)
        case 'datetime' : newDateTimeProperty(symbol)
        default : newEnumProperty(symbol, dto)
    }
    ;
    
/** Add DtoProperty objects to 'dto' for each External PropertyReference in 'dto'
 */    
completeProperties(BusinessClassDto dto) :
    let symbol = lookupSymbol(dto.base.modelname, dto.base.name, "BusinessClass") :
    let propertySymbols = symbol.subSymbols.select(s|s.type == "Property") :
//    print("DTO completeProperties: "+ dto.name)->
    dto.properties.addAll( dto.propertyReferences.referencesToDtoProperty(propertySymbols) )  
    ;
    
addDtoAssociationRoles(BusinessClassAssociationRoleReference assRef) :
    let symbol = lookupSymbol(assRef.dto.base.modelname, assRef.dto.base.name, "BusinessClass") :
    let prop = symbol.subSymbols.select(s|s.type.matches("Association") && (s.name == assRef.name)).first() :
    let result = new DtoAssociationRoleProperty :
//    print("DTO ASSOCIATION : "+ assRef.name + " for dto [" + assRef.dto.name + "]")->
    result.setName(prop.name)->
    result.setDataType(assRef.dtoType.name)->
    result.setDtoType(assRef.dtoType)->
    assRef.dto.properties.add(result)
   ;
    
/** Create a new DtoProperty for 'ref''
 *  'symbols' is the list of Symbols from Crossx containing the "Property" definitions
 */
DataContractDsl::DtoProperty referencesToDtoProperty(BusinessClassPropertyReference ref, List[Symbol] symbols ) :
    let symbol = symbols.select(s| s.name == ref.name).first() :
    if symbol != null then {
	    switch (symbol.getPropertyValue("DataType")) {
	        case 'boolean' : newBooleanProperty(symbol)
	        case 'string' : newStringProperty(symbol)
	        case 'integer' : newIntegerProperty(symbol)
	        case 'decimal' : newDecimalProperty(symbol)
	        case 'datetime' : newDateTimeProperty(symbol)
	        default : newEnumProperty(symbol, ref.dto)
	    }
    }
    else {
        print("referencesToDtoProperty: DTO null for ref " + ref.name )
    }
    
    ;
    
DataContractDsl::DtoBooleanProperty newBooleanProperty(Symbol symbol) :
    let result = new DtoBooleanProperty :
    let nullable = symbol.getPropertyValue("Nullable") :
    result.setName(symbol.name)->
    result.setDataType("boolean")->
    result.setNullable( nullable == "true" )->
    result;
    
DataContractDsl::DtoStringProperty newStringProperty(Symbol symbol) :
    let result = new DtoStringProperty:
    let nullable = symbol.getPropertyValue("Nullable") :
    result.setName(symbol.name)->
    result.setDataType("string")->
    result.setNullable( nullable == "true" )->
    result;
    
DataContractDsl::DtoIntegerProperty newIntegerProperty(Symbol symbol) :
    let result = new DtoIntegerProperty:
    let nullable = symbol.getPropertyValue("Nullable") :
    result.setName(symbol.name)->
    result.setDataType("integer")->
    result.setNullable( nullable == "true" )->
    result;
    
DataContractDsl::DtoDecimalProperty newDecimalProperty(Symbol symbol) :
    let result = new DtoDecimalProperty :
    let nullable = symbol.getPropertyValue("Nullable") :
    result.setName(symbol.name)->
    result.setDataType("decimal")->
    result.setNullable( nullable == "true" )->
    result;
    
DataContractDsl::DtoDateTimeProperty newDateTimeProperty(Symbol symbol) :
    let result = new DtoDateTimeProperty:
    let nullable = symbol.getPropertyValue("Nullable") :
    result.setName(symbol.name)->
    result.setDataType("datetime")->
    result.setNullable( nullable == "true" )->
    result;

// Create a new DtoEnumerationProperty based on 'symbol'
DataContractDsl::DtoEnumerationProperty newEnumProperty(Symbol symbol, BusinessClassDto dto) :
    let result = new DtoEnumerationProperty:
    let dataType = symbol.getPropertyValue("DataType") :
    let enum = dto.datacontractModel.enumerations.selectFirst(e|(e.base != null) &&
                                                    (e.base.name == dataType) ):
    let nullable = symbol.getPropertyValue("Nullable") :
    result.setDataType( symbol.getPropertyValue("DataType") )->
    ( if enum != null then (
          result.setName(symbol.name)->
          result.setType(enum)->
          result.setDataType(enum.name)->
          result.setNullable( nullable == "true" )
      ) else (
          result.setName("NO_NAME")->
	      result.setType(null)->
          result.setDataType("NO_DATA_TYPE")
      )
     )->
    result;
