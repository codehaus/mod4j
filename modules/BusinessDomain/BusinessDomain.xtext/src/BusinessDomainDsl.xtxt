preventMMGeneration

importMetamodel "http://www.mod4j.org/busmod" ;

//specify your DSL grammar rules here ...
//IMPORTANT: You should change the property 'overwrite.pluginresources=true' in the properties file to 'overwrite.pluginresources=false' AFTER first generation

BusinessDomainModel :
	("Description" description=STRING )?
	"Model" name = ID
	(types += BusinessClass)* 
	(enumerations += Enumeration)*
	(associations += Association)*;

Association:
	("Description" description=STRING )?
    "Association" 
    (composite?="composite")?
    sourceRoleName=ID source=[BusinessClass] "[" sourceMultiplicity=Multiplicity "]" "<->"
    "[" targetMultiplicity=Multiplicity "]"targetRoleName=ID target=[BusinessClass] ";";

Enum Multiplicity  :
     ZeroMany="0..*"  | One="1" | OneMany="1..*" | ZeroOne="0..1" ;
       	
BusinessClass :
	("Description" description=STRING )?
    "Class" name=ID
    (inheritanceModifier=InheritanceModifier)?
    ("inherits" superclass=[BusinessClass])?
    "["
    ( properties += Property)*
    ("Rules" "[" (businessRules += BusinessRule)* "]" )?
    ("Constraints" "[" (constraints += Constraint)* "]")?
    "]"
     ;
     
Enumeration :
	("Description" description=STRING )?
    "Enumeration" name=ID "["
    (enumerationLiterals += EnumerationLiteral)+ "]";

EnumerationLiteral:
    name=ID ( "=" persistedValue=ID)? ";" ;
    
BusinessRule :
    name=ID ";" ;
    
Constraint :
    name=ID  ";" ;
    
Enum InheritanceModifier :
    ABSTRACT="abstract" | FINAL="final";
    
Property : 
	(BoolProperty | StringProperty | DateTimeProperty) 
	(nullable?="nullable")?
	("default" defaultValue=STRING)?
;

DateTimeProperty : "Timestamp" (accuracy=DateTimeAccuracy)? name=ID ";" ;

Enum DateTimeAccuracy :
    Month="Month" | Year="Year" | Day = "Day";

BoolProperty : "Boolean" name=ID ";"
	;

StringProperty : "String" name=ID ";"
;

