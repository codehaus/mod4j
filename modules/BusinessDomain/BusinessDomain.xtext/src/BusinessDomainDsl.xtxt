preventMMGeneration

importMetamodel "http://www.mod4j.org/busmod" ;

//IMPORTANT: You should change the property 'overwrite.pluginresources=true' in the properties file to 'overwrite.pluginresources=false' AFTER first generation

BusinessDomainModel :
	(description=STRING )?
	"Model" name = ID
	(types += BusinessClass)* 
	(enumerations += Enumeration)*
	(associations += Association)*;

Association:
	(description=STRING )?
    "Association" 
    (composite?="composite")?
    sourceRoleName=ID source=[BusinessClass] "[" sourceMultiplicity=Multiplicity "]"
    ( (bidirectional?="<->") | ("->") )
    "[" targetMultiplicity=Multiplicity "]"targetRoleName=ID target=[BusinessClass] ";";

Enum Multiplicity  :
     ZeroMany="0..*"  | One="1" | OneMany="1..*" | ZeroOne="0..1" ;
       	
BusinessClass :
	(description=STRING )?
    "Class" name=ID
    (inheritanceModifier=InheritanceModifier)?
    ("inherits" superclass=[BusinessClass])?
    "["
    ( properties += Property)*
    ("Rules" "[" (businessRules += BusinessRule)* "]" )?
    "]"
     ;
     
Enumeration :
	(description=STRING )?
    "Enumeration" name=ID "["
    (enumerationLiterals += EnumerationLiteral)+ "]";

EnumerationLiteral:
    name=ID ( "=" persistedValue=INT)? ";" ;
    
BusinessRule :
	(description=STRING )?
    name=ID ";" ;
    
Enum InheritanceModifier :
    ABSTRACT="abstract" | FINAL="final";
    
Property : 
    (description=STRING )?
	(BoolProperty | StringProperty | DateTimeProperty | IntegerProperty |
	 DecimalProperty | EnumerationProperty ) 
	(nullable?="nullable")?
	("persistency" persistency=PersistencyMode)?
	";"
	
;
Enum PersistencyMode :
    ReadWrite="ReadWrite" | Read="Read" | None= "NONE";

DateTimeProperty : dataType="datetime" accuracy=DateTimeAccuracy name=ID
                 (hasDefault?="default" defaultValue=STRING)?;

Enum DateTimeAccuracy :
    Month="month" | Year="year" | Day = "day" | Hour = "hour" | Minute = "minute" |
    Second = "second" | Millisecond = "millisecond";

BoolProperty : dataType="boolean" name=ID
               (hasDefault?="default" ((defaultValue?="true") | "false"))?
            ;

StringProperty : dataType="string" name=ID
                 (hasDefault?="default" defaultValue=STRING)?
                 ("minlength" minLength=INT)? ("maxlength" maxLength=INT)? 
                 ;

IntegerProperty : (dataType="integer") name=ID
                 (hasDefault?="default" defaultValue=INT)?
                 ("min" minValue=INT)? ("max" maxValue=INT)?  
                 ; 

DecimalProperty : (dataType="decimal" ) name=ID
//                 (hasDefault?="default" defaultValue=INT)?
//               ("min" minValue=INT)? ("max" maxValue=INT)?  
                 ; 

EnumerationProperty : type=[Enumeration] name=ID
                 (hasDefault?="default" defaultValue=STRING)?

; 
