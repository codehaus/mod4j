grammar org.mod4j.dsl.presentation.xtext.Presentation with org.eclipse.xtext.common.Terminals

import "platform:/resource/org.mod4j.dsl.presentation.mm/model/PresentationDsl.ecore"

//IMPORTANT: You should change the property 'overwrite.pluginresources=true' in the properties file to 'overwrite.pluginresources=false' AFTER first generation

PresentationModel :
	(description=STRING )?
	"presentation" name = ID ";"
	(externalReferences += ExternalReference)*
	(elements += ModelElementWithContext)*
	;

ExternalReference :
    "from" modelName = ID "import" name = ID ";"
	;
	
ModelElementWithContext :
	UIModelElement //| Link
	;
	
UIModelElement :
	Dialogue | Process
	;
		
Dialogue :
	ContentForm | CompoundDialogue
	;
	
ContentForm :
	(description=STRING )?
	( (isCollection?="ListForm") |
	  "ContentForm"
	)
	
	 name = ID "context" contextRef = [ExternalReference]
	"["
		("readonly" ((readonly?="true") | "false") ";")?
		(formElements += FormElement)*
		("actions" "[" (actions += SimpleProcess ";" )* "]" )?
		("processes" "[" (processes += ProcessCall ";")* "]" )?
	"]"
	;

SimpleProcess :
	(description=STRING )? 
    name=ID (type=ProcessType)?
    (
        ( dialogueProcess = InteractiveProcessAction )
      |
        ( process = AutomatedProcessAction )
    )
    ;

AutomatedProcessAction returns AutomatedProcess :
	("services" "[" services += ServiceExpression ("," services += ServiceExpression)? "]")
	|
	"batch"
	; 
 
InteractiveProcessAction returns InteractiveProcess :
	("use" processElements += DialogueCall ) 
	;
  	
enum ProcessType :
    NEW = "new" | SAVE = "save" | EDIT = "edit" | REMOVE = "remove"
;

CompoundDialogue :
	(description=STRING )?
	"CompoundDialogue" name = ID "context" contextRef = [ExternalReference]
	("readonly" ((readonly?="true") | "false") ";")?
	
		("dialogues" "[" (dialogues += DialogueCall ";")* "]" )?
		("processes" "[" (processes += ProcessCall ";")* "]" )?  
		| CollectionDialogue
		| MasterDetail
	
	;
		
CollectionDialogue :
	(description=STRING )?
	"CollectionDialogue" name = ID "context" contextRef = [ExternalReference]
	"["
		("readonly" ((readonly?="true") | "false") ";")?
		("dialogues" "[" (dialogues += DialogueCall ";")* "]" )?
		("processes" "[" (processes += ProcessCall ";")* "]" )?
	"]"
	;
	
MasterDetail :
	(description=STRING )?
	"MasterDetail" name = ID "context" contextRef = [ExternalReference]
	("readonly" ((readonly?="true") | "false") ";")?
	"master" master = DialogueCall ";"
	"detail" detail = DialogueCall ";"
	("processes" "[" (processes += ProcessCall ";")* "]" )?
	;

Process :
	AutomatedProcess | InteractiveProcess
	;
	
AutomatedProcess :
	(description=STRING )?
	"AutomatedProcess" name = ID "context" contextRef = [ExternalReference]
	(type=ProcessType)?
	("steps" "[" (processElements += UICall ";")* "]")?
	("services" "[" services += ServiceExpression ("," services += ServiceExpression )? "]"
	)?
	;

InteractiveProcess :
	(description=STRING )?
	"InteractiveProcess" name = ID "context" contextRef = [ExternalReference]
	(type=ProcessType)?
//	("steps" "[" (processElements += UICall ";")* "]")?
	("step" "[" (processElements += DialogueCall ";") "]")
	;
	
FormElement :
	(description=STRING )?
	"element" 
//	("navigate" LinkRef "to")? 
	references = DtoPropertyReference 
	"label" label = ID 
	("readonly" ((readonly?="true") | "false") )?
	";"
	;
	
DialogueCall :
	(contextExp = Expression "to")? name = ID
	("alias" alias = ID)? 
	("(" (arguments += Expression) ("," arguments += Expression)*  ")")?
	;

Expression :
    ServiceExpression | NavigationExpression
    ;
   	
ProcessCall :
	(contextExp = Expression "to")? name = ID
	("alias" alias = ID)? 
	("(" (arguments += Expression) ("," arguments += Expression)*  ")")?
	;

ServiceExpression :
    (name = ID ":")? "call"  serviceName = ID "." serviceMethod = ID
    ;

NavigationExpression :
    (name = ID ":")? "navigate" references += AssociationRoleReference  
    ( "." references += AssociationRoleReference ) *
    ;

DtoPropertyReference :
	name = ID
	;

AssociationRoleReference :
	name = ID
	;
	
UICall :
	UIModelElementCall
	;
	
UIModelElementCall :
	"dialogue" DialogueCall | "process" ProcessCall
	;
	