grammar org.mod4j.dsl.presentation.xtext.Presentation with org.eclipse.xtext.common.Terminals

import "platform:/resource/org.mod4j.dsl.presentation.mm/model/PresentationDsl.ecore"

//IMPORTANT: You should change the property 'overwrite.pluginresources=true' in the properties file to 'overwrite.pluginresources=false' AFTER first generation

PresentationModel :
	(description=STRING )?
	"presentation" name = ID ";"
	(externalReferences += ExternalReference)*
	(elements += ModelElementWithContext)*
	;

ExternalReference :
    "from" modelName = ID "import" name = ID ";"
	;
	
ModelElementWithContext :
	UIModelElement | Link
	;
	
UIModelElement :
	Dialogue | Process
	;
		
Dialogue :
	ContentForm | CompoundDialogue
	;
	
ContentForm :
	(description=STRING )?
	"ContentForm" name = ID "context" contextRef = [ExternalReference]
	"["
		("readonly" ((readonly?="true") | "false") ";")?
		(formElements += FormElement)*
		("actions" "[" (actions += SimpleProcess  ";")* "]" )?
		("processes" "[" (processes += ProcessCall ";")* "]" )?
	"]"
	;

SimpleProcess :
    name=ID 
    ( "use" dialogue = DialogueCall  )? 
    ;
    	
CompoundDialogue :
	(description=STRING )?
	"CompoundDialogue" name = ID "context" contextRef = [ExternalReference]
	("readonly" ((readonly?="true") | "false") ";")?
	
		("dialogues" "[" (dialogues += DialogueCall ";")* "]" )?
		("processes" "[" (processes += ProcessCall ";")* "]" )?  
		| CollectionDialogue
		| MasterDetail
	
	;
		
CollectionDialogue :
	(description=STRING )?
	"CollectionDialogue" name = ID "context" contextRef = [ExternalReference]
	"["
		("readonly" ((readonly?="true") | "false") ";")?
		("dialogues" "[" (dialogues += DialogueCall ";")* "]" )?
		("processes" "[" (processes += ProcessCall ";")* "]" )?
	"]"
	;
	
MasterDetail :
	(description=STRING )?
	"MasterDetail" name = ID "context" contextRef = [ExternalReference]
	("readonly" ((readonly?="true") | "false") ";")?
	"master" master = DialogueCall ";"
	"detail" detail = DialogueCall ";"
	("processes" "[" (processes += ProcessCall ";")* "]" )?
	;

Process :
	AutomatedProcess | InteractiveProcess
	;
	
AutomatedProcess :
	(description=STRING )?
	"AutomatedProcess" name = ID "context" contextRef = [ExternalReference]
	("steps" "[" (processElements += UICall ";")* "]")?
	;

InteractiveProcess :
	(description=STRING )?
	"InteractiveProcess" name = ID "context" contextRef = [ExternalReference]
	("steps" "[" (processElements += UICall ";")* "]")?
	;
	
FormElement :
	(description=STRING )?
	"element" 
//	("navigate" LinkRef "to")? 
	references = DtoPropertyReference 
	"label" label = ID 
	("readonly" ((readonly?="true") | "false") )?
	";"
	;
		
DialogueCall :
	(DirectDialogueCall | LinkedDialogueCall) ("alias" alias = ID)? 
	;
	
LinkedDialogueCall :
	"navigate" link = LinkRef "to" name = ID
	;

DirectDialogueCall :
	name = ID
	;
	
ProcessCall :
	(DirectProcessCall | LinkedProcessCall) ("alias" alias = ID)? 
	;

LinkedProcessCall :
	"navigate" link = LinkRef "to" name = ID 
	;

DirectProcessCall :
	name = ID
	;
	
LinkRef :
	name = ID
	;
	
Link :
	LinkStep | LinkPath
	;
	
LinkStep :
	(description=STRING )?
	"LinkStep" name = ID "context" contextRef = [ExternalReference] 
	"navigate" reference = AssociationRoleReference
	;
	
LinkPath :
	(description=STRING )?
	"LinkPath" name = ID "context" contextRef = [ExternalReference]
	;
	
DtoPropertyReference :
	name = ID
	;

AssociationRoleReference :
	name = ID
	;
	
UICall :
	UIModelElementCall
	;
	
UIModelElementCall :
	"dialogue" DialogueCall | "process" ProcessCall
	;
	