grammar org.mod4j.dsl.presentation.xtext.Presentation with org.eclipse.xtext.common.Terminals

import "platform:/resource/org.mod4j.dsl.presentation.mm/model/PresentationDsl.ecore"

//IMPORTANT: You should change the property 'overwrite.pluginresources=true' in the properties file to 'overwrite.pluginresources=false' AFTER first generation

PresentationModel :
    ApplicationModel | FormsAndProcesses;
   

ApplicationModel returns PresentationModel :
    (description=STRING )?
    "application" name = ID ";"
    
    (externalReferences += ProcessReference)*
    (mainMenu = Menu)
;

ProcessReference : 
    "from" modelName = ID "process" name = ID ";"
    ;

FormsAndProcesses returns PresentationModel :
	(description=STRING )?
	"presentation" name = ID ";"
	
	(externalReferences += ExternalReference)*
	(elements += ModelElementWithContext)*
	;

Menu :
    "application" "["
    startProcesses  += SimpleProcessCall ";"
    (startProcesses += SimpleProcessCall ";" )*
    "]"
;

ExternalReference :
    DtoReference | ProcessReference | DialogueReference | ServiceReference
	;

DtoReference : 
    "from" modelName = ID "dto" name = ID ";"
    ;
    
ServiceReference : 
    "from" modelName = ID "service" name = ID ";"
    ;
    
DialogueReference : 
    "from" modelName = ID "form" name = ID ";"
    ;
    
ModelElementWithContext :
	UIModelElement //| Link
	;
	
UIModelElement :
	Dialogue | Process
	;
		
Dialogue :
	ContentForm | CompoundDialogue
	;
	
ContentForm :
	(description=STRING )?
    "form"
	
	 name = ID "context" (collectionContext ?= "list")? contextRef = [DtoReference]
	"["
		("readonly" ((readonly?="true") | "false") ";")?
		(formElements += FormElement)*
		("actions" "[" (actions += Action ";" )* "]" )?
		("processes" "[" (processes += SimpleProcessCall ";")* "]" )?
	"]"
	;

Action  returns Process:
	(description=STRING )? 
    (type=ProcessType)? 
    name=ID 
    ("label" label = STRING)?
	(processElements += ActionDialogueCall ) ?
    ;
  	
enum ProcessType :
    NEW = "new" | SAVE = "save" | EDIT = "edit" | DELETE = "delete" | CANCEL = "cancel" |
    REMOVEFROM = "removefrom" | ADDTO = "addto" 
;

enum ProcessTypeLink returns ProcessType :
    LINK = "link"
;

CompoundDialogue :
	(description=STRING )?
	"CompoundDialogue" name = ID "context" contextRef = [DtoReference]
	("readonly" ((readonly?="true") | "false") ";")?
	
		("dialogues" "[" (dialogues += DialogueCall ";")* "]" )?
		("processes" "[" (processes += ProcessCall ";")* "]" )?  
		| CollectionDialogue
		| MasterDetail
	
	;
		
CollectionDialogue :
	(description=STRING )?
	"CollectionDialogue" name = ID "context" contextRef = [DtoReference]
	"["
		("readonly" ((readonly?="true") | "false") ";")?
		("dialogues" "[" (dialogues += DialogueCall ";")* "]" )?
		("processes" "[" (processes += ProcessCall ";")* "]" )?
	"]"
	;
	
MasterDetail :
	(description=STRING )?
	"MasterDetail" name = ID "context" contextRef = [DtoReference]
	("readonly" ((readonly?="true") | "false") ";")?
	"master" master = DialogueCall ";"
	"detail" detail = DialogueCall ";"
	("processes" "[" (processes += ProcessCall ";")* "]" )?
	;

Process :
	(description=STRING )?
	"process" 
    (   (type=ProcessType) | 
        (type=ProcessTypeLink link=AssociationRoleReference ) 
    )?	
	name = ID "context" (collectionContext ?= "list")? contextRef = [DtoReference]
	(
	    ";"
	    |
	    (	"["
            ( processElements += UICall ";")* 
	        "]"
	     )
	 )
	;
	
FormElement :
	(description=STRING )?
	"element" 
//	("navigate" LinkRef "to")? 
	references = DtoPropertyReference 
	("label" label = STRING )?
	("readonly" ((readonly?="true") | "false") )?
	";"
	;
	
ActionDialogueCall returns DialogueCall:
    (contextExp = Expression)? "form" 
    referredDialogue = [AbstractDialogue]
//    ("(" (arguments += Expression) ("," arguments += Expression)*  ")")?
    ;

DialogueCall :
    (label = STRING  ":" )?
    ("if" "("  condition = OperationExpression ")")?
    (contextExp = Expression )? "form" 
    referredDialogue = [AbstractDialogue]
//    ("alias" alias = ID)? 
//    ("(" (arguments += Expression) ("," arguments += Expression)*  ")")?
    ;

Expression :
    ServiceExpression | NavigationExpression | StandardExpression | OperationExpression
    ;
   	
ProcessCall :
    (label = STRING  ":" )?
    ("if" "("  condition = OperationExpression ")")?
    (contextExp = Expression)? 
     "process" referredProcess = [AbstractProcess]
    ("label" label = ID)? 
//    ("(" (arguments += Expression) ("," arguments += Expression)*  ")")?
    ;
    
OperationExpression :
    "call" name = ID;
    
SimpleProcessCall returns ProcessCall :
    referredProcess = [AbstractProcess]
//     name = ID
    ("label" label = STRING)? 
//    ("(" (arguments += Expression) ("," arguments += Expression)*  ")")?
    ;

StandardExpression :
    type = ExpressionType;
    
enum ExpressionType :
    ALL = "all" | FIND = "find";

ServiceExpression :
    "service"  service = [ServiceReference] 
    ;

NavigationExpression :
    (composition ?= "composite")? "navigate" references += AssociationRoleReference  
// TODO: Only single reference for now, should become multiple    ( "." references += AssociationRoleReference ) *
    ;

DtoPropertyReference :
	name = ID
	;

AssociationRoleReference :
	name = ID
	;
	
UICall :
	DialogueCall |  ProcessCall
	;
	